


plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}
apply plugin: 'maven-publish'

group 'io.github.jwdeveloper.exaple'
version '1.1.1'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}
sourceCompatibility = 1.8

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.8'
    testImplementation platform("org.spockframework:spock-bom:2.0-groovy-2.5")
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}


java {
    withJavadocJar()
    withSourcesJar()
}

publishing  {
    publications{
        mavenJava(MavenPublication){

            groupId = 'io.github.jwdeveloper.exaple'
            artifactId = 'example-library'
            version = '1.1.1'
            from components.java

            pom {
                name = 'example'
                description = 'Example upload to mave central repository'
                url = 'https://github.com/jwdeveloper/ExampleGrandleProject'
                inceptionYear = '2022'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'jw_developer'
                        name = 'JW'
                        email = 'example@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/jwdeveloper/ExampleGrandleProject'
                    developerConnection = 'scm:git:ssh://github.com/jwdeveloper/ExampleGrandleProject'
                    url = 'https://github.com/jwdeveloper/ExampleGrandleProject'
                }

            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}